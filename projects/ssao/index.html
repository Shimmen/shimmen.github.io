<!DOCTYPE html>
<html>
	<head>
		<meta charset="utf-8">
		<title>ssao – simon moos</title>
		<meta name="viewport" content="width=device-width, initial-scale=1.0">
		<link rel="shortcut icon" href="../../favicon.ico">
		<link rel="stylesheet" href="../../css/project.css">
	</head>
	<body>

		<header>
			<h1><a href="/">Simon Moos</a></h1>
		</header>

		<section>

			<h1>
				<span style="text-transform: none;">SSAO</span> demo
				<small>2017-11-29 – Simon Moos</small>
			</h1>

			<canvas id="canvas"></canvas>
			<button type="button" id="fullscreen-btn" class="remove-if-no-gl" onclick="goFullscreen()">Go Fullscreen</button>
			<div class="remove-if-no-gl">
				<div>
					<label>SSAO kernel radius:</label>
					<input id="kernelRadiusSlider" type="range" min="0.02" max="0.8" step="any">
					<label id="kernelRadiusLabel"></label>
				</div>
				<div>
					<label>SSAO kernel count:</label>
					<input id="kernelCountSlider" type="range" min="1" max="64" step="1">
					<label id="kernelCountLabel"></label>
				</div>
				<div>
					<label>SSAO bias:</label>
					<input id="ssaoBiasSlider" type="range" min="-0.25" max="0.25" step="any">
					<label id="ssaoBiasLabel"></label>
				</div>
				<div>
					<label>SSAO power:</label>
					<input id="ssaoPowerSlider" type="range" min="1.0" max="10.0" step="any">
					<label id="ssaoPowerLabel"></label>
				</div>
				<div>
					<label>Direct lighting:</label>
					<input type="checkbox" id="directLightingCheckbox" checked="true">
				</div>
				<div>
					<label>SSAO enabled:</label>
					<input type="checkbox" id="ssaoEnabledCheckbox" checked="true">
				</div>
			</div>

			<script type="x-shader/x-vertex" id="geometry-vs">
				#version 300 es

				layout(location = 0) in vec3 a_position;
				layout(location = 1) in vec3 a_normal;

				layout(std140) uniform CameraUniforms {
					mat4 u_view_from_world;
					mat4 u_projection_from_view;
					mat4 u_view_from_projection;
				};

				uniform mat4 u_world_from_local;

				out vec3 v_normal;

				void main() {
					v_normal = (u_view_from_world * u_world_from_local * vec4(a_normal, 0.0)).xyz; // only perform uniform scaling!
					gl_Position = u_projection_from_view * u_view_from_world * u_world_from_local * vec4(a_position, 1.0);
				}
			</script>
			<script type="x-shader/x-fragment" id="geometry-fs">
				#version 300 es
				precision highp float;

				in vec3 v_normal;

				layout(location = 0) out vec4 o_frag_color;
				layout(location = 1) out vec4 o_view_normal;

				void main() {
					// TODO: Read from texture, etc.
					o_frag_color = vec4(230.0 / 256.0, 228.0 / 256.0, 216.0 / 256.0, 1.0);

					vec3 N = normalize(v_normal);
					vec3 packed_view_normal = N * vec3(0.5) + vec3(0.5);
					o_view_normal = vec4(packed_view_normal, 0.0);
				}
			</script>

			<script type="x-shader/x-vertex" id="ssao-vs">
				#version 300 es

				layout(location = 0) in vec2 a_position;
				out vec2 v_tex_coord;

				void main() {
					v_tex_coord = a_position * vec2(0.5) + vec2(0.5);
					gl_Position = vec4(a_position, 0, 1);
				}
			</script>
			<script type="x-shader/x-fragment" id="ssao-fs">
				#version 300 es
				precision highp float;

				layout(std140) uniform CameraUniforms {
					mat4 u_view_from_world;
					mat4 u_projection_from_view;
					mat4 u_view_from_projection;
				};

				uniform sampler2D u_normal_texture;
				uniform sampler2D u_depth_texture;

				const int SSAO_KERNEL_MAX_SIZE = 64;
				uniform int u_ssao_kernel_size;
				uniform vec3 u_ssao_kernel[SSAO_KERNEL_MAX_SIZE];
				uniform sampler2D u_ssao_noise_texture;
				uniform vec2 u_ssao_noise_scale;
				uniform float u_ssao_radius;
				uniform float u_ssao_bias;
				uniform float u_ssao_power;

				in vec2 v_tex_coord;
				layout(location = 0) out vec4 o_frag_color;

				vec3 project(vec3 vs_pos) {
					vec4 proj_pos = u_projection_from_view * vec4(vs_pos, 1.0);
					proj_pos.xyz /= proj_pos.w;
					proj_pos.z = proj_pos.z * 0.5 + 0.5;
					return proj_pos.xyz;
				}

				vec3 unproject(vec3 proj_pos) {
					vec4 vs_pos = u_view_from_projection * vec4(proj_pos, 1.0);
					vs_pos.xyz /= vs_pos.w;
					return vs_pos.xyz;
				}

				void main() {
					vec4 norm_depth = texture(u_normal_texture, v_tex_coord);
					vec3 normal = norm_depth.xyz * vec3(2.0) - vec3(1.0);
					float depth = texture(u_depth_texture, v_tex_coord).r;

					// Get view space position of fragment
					vec3 origin = unproject(vec3(v_tex_coord * vec2(2.0) - vec2(1.0), depth * 2.0 - 1.0));
					origin += normal * vec3(0.025); // some amount of normal offset that works

					// Set up tbn matrix for orienting the noise kernel (with random rotation from noise texture)
					vec2 rvec_coords = v_tex_coord * u_ssao_noise_scale;
					vec3 rvec = vec3(texture(u_ssao_noise_texture, rvec_coords).xy * vec2(2.0) - vec2(1.0), 0.0);
					vec3 tangent = normalize(rvec - normal * dot(rvec, normal));
					vec3 bitangent = cross(tangent, normal);
					mat3 tbn = mat3(tangent, bitangent, normal);

					// Perform SSAO kernel convolution
					float occlusion = 0.0;
					for (int i = 0; i < u_ssao_kernel_size; ++i) {

						// Calculate sample view space position
						vec3 sample_pos = tbn * u_ssao_kernel[i];
						sample_pos = origin + (sample_pos * u_ssao_radius);
						float vs_sample_depth = sample_pos.z;

						// Get actual/reference depth at sample
						vec3 proj_sample_pos = project(sample_pos);
						vec2 sample_uv = proj_sample_pos.xy * vec2(0.5) + vec2(0.5);
						float proj_reference_depth = texture(u_depth_texture, sample_uv).r;
						vec3 ref_vs_position = unproject(vec3(proj_sample_pos.xy, proj_reference_depth * 2.0 - 1.0));
						float vs_reference_depth = ref_vs_position.z;

						// TODO: Make non-branching variants of these!
						float range_check = abs(origin.z - vs_reference_depth) < u_ssao_radius ? 1.0 : 0.0;
						occlusion += (vs_sample_depth < vs_reference_depth ? 1.0 : 0.0) * range_check;
					}

					occlusion = occlusion / float(u_ssao_kernel_size);
					occlusion = 1.0 - pow(1.0 - occlusion + u_ssao_bias, u_ssao_power);

					o_frag_color = vec4(vec3(occlusion), 1.0);
				}
			</script>

			<script type="x-shader/x-vertex" id="post-vs">
				#version 300 es

				layout(location = 0) in vec2 a_position;
				out vec2 v_tex_coord;

				void main() {
					v_tex_coord = a_position * vec2(0.5) + vec2(0.5);
					gl_Position = vec4(a_position, 0, 1);
				}
			</script>
			<script type="x-shader/x-fragment" id="post-fs">
				#version 300 es
				precision highp float;

				uniform sampler2D u_albedo;
				uniform sampler2D u_normal;
				uniform sampler2D u_occlusion;
				uniform int u_blur_size;
				uniform bool u_enable_direct_lighting;
				uniform bool u_enable_ssao;

				in vec2 v_tex_coord;
				layout(location = 0) out vec4 o_frag_color;

				float getOcclusionFactor() {
					// Blur the occlusion to accomidate the ssao noise texture

					vec2 texel_size = 1.0 / vec2(textureSize(u_occlusion, 0));
					int loop_size = u_blur_size / 2;

					float result = 0.0;
					for (int i = -loop_size; i < loop_size; ++i) {
						for (int j = -loop_size; j < loop_size; ++j) {
							vec2 offset = vec2(float(i), float(j)) * texel_size;
							result += texture(u_occlusion, v_tex_coord + offset).r;
						}
					}
					float occlusion = result / float(u_blur_size * u_blur_size);

					// pow:ing the occlusion like this seems to make the blur-noise less visible,
					// but it also obviously affects the appearance of the SSAO.
					return occlusion * occlusion;
				}

				const vec3 view_light_direction = vec3(-1, 0, -1);

				void main() {

					vec3 packed_view_normal = texture(u_normal, v_tex_coord).rgb;
					vec3 N = normalize(packed_view_normal * vec3(2.0) - vec3(1.0));

					const vec3 L = normalize(-view_light_direction);
					float LdotN = max(dot(L, N), 0.0);

					vec3 color = texture(u_albedo, v_tex_coord).rgb;
					color *= (u_enable_direct_lighting) ? max(LdotN, 0.25) : 1.0;
					color *= (u_enable_ssao) ? (1.0 - getOcclusionFactor()) : 1.0;

					o_frag_color = vec4(color, 1);
				}
			</script>

			<h2>About</h2>
			<p>A simple little demo of WebGL <b>screen space ambient occlusion</b>, i.e. SSAO. The method I used here is the <b>normal‑oriented hemisphere</b> method which is described by John Chapman in <a href="http://john-chapman-graphics.blogspot.se/2013/01/ssao-tutorial.html">his article</a> in high detail. For that reason I've also based most of this demo on that article.</p>
			<p><a href="https://twitter.com/share" class="twitter-share-button" data-text="WebGL SSAO demo" data-via="SimonMoos" data-lang="en" data-show-count="false">Tweet</a></p>
			<p>If you want to see and hear more from me, follow me on Twitter and GitHub:</p>
			<p>
				<a href="https://twitter.com/SimonMoos" class="twitter-follow-button" data-lang="en" data-show-count="false">Follow @SimonMoos</a>
				<iframe src="https://ghbtns.com/github-btn.html?user=Shimmen&type=follow&count=false" frameborder="0" scrolling="0" width="170px" height="20px"></iframe>
			</p>

		</section>

		<footer>
			<p>Simon Moos (c) 2017</p>
		</footer>

		<script src="../picogl.min.js" charset="utf-8"></script>
		<script src="../gl-matrix-min.js" charset="utf-8"></script>
		<script src="../webgl-obj-loader.js" charset="utf-8"></script>
		<script src="rendering.js" charset="utf-8"></script>
		<script src="../base.js" charset="utf-8"></script>

		<script async src="//platform.twitter.com/widgets.js" charset="utf-8"></script>
		<script>
			(function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
			(i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
			m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
			})(window,document,'script','https://www.google-analytics.com/analytics.js','ga');
			ga('create', 'UA-98613739-1', 'auto');
			ga('send', 'pageview');
		</script>
	</body>
</html>
